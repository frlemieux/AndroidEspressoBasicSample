apply plugin: "com.android.application"
apply plugin: "kotlin-android"

android {
    compileSdk 34
    defaultConfig {
        applicationId "com.example.android.testing.espresso.BasicSample"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    productFlavors {
    }
    buildFeatures {
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.14"
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        managedDevices {
            devices {

                // run with ../gradlew  nexusOneApi30DebugAndroidTest
                nexusOneApi30(com.android.build.api.dsl.ManagedVirtualDevice) {
                    // A lower resolution device is used here for better emulator performance
                    device = "Nexus One"
                    apiLevel = 30
                    // Also use the AOSP ATD image for better emulator performance
                    systemImageSource = "aosp-atd"
                }
            }
        }
    }
    namespace "com.example.android.testing.espresso.BasicSample"
    testNamespace "com.example.android.testing.espresso.BasicSample.test"
    lint {
        abortOnError false
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {

    Dependency composeBom = platform('androidx.compose:compose-bom:2024.10.00')
    implementation composeBom
    testImplementation composeBom
    androidTestImplementation composeBom
    // Choose one of the following:
    // Material Design 3
    implementation("androidx.compose.material3:material3")
    // or skip Material Design and build directly on top of foundational components
    implementation("androidx.compose.foundation:foundation")
    // or only import the main APIs for the underlying toolkit systems,
    // such as input and measurement/layout
    implementation("androidx.compose.ui:ui")

    // Android Studio Preview support
    implementation("androidx.compose.ui:ui-tooling-preview")
    debugImplementation("androidx.compose.ui:ui-tooling")

    // UI Tests
    androidTestImplementation("androidx.compose.ui:ui-test-junit4")
    debugImplementation("androidx.compose.ui:ui-test-manifest")
    // Optional - Integration with activities
    implementation("androidx.activity:activity-compose:1.9.3")



    // App dependencies
    implementation "androidx.annotation:annotation:" + rootProject.androidxAnnotationVersion;
    implementation "com.google.guava:guava:" + rootProject.guavaVersion

    // Testing-only dependencies
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    androidTestImplementation "androidx.test:core:" + rootProject.coreVersion
    androidTestImplementation "androidx.test:core-ktx:" + rootProject.coreVersion
    androidTestImplementation "androidx.test.ext:junit:" + rootProject.extJUnitVersion
    androidTestImplementation "androidx.test.ext:junit-ktx:" + rootProject.extJUnitVersion
    androidTestImplementation "androidx.test:runner:" + rootProject.runnerVersion
    androidTestImplementation "androidx.test.espresso:espresso-core:" + rootProject.espressoVersion

    testImplementation "androidx.test:core:" + rootProject.coreVersion;
    testImplementation "androidx.test.ext:junit:" + rootProject.extJUnitVersion
    testImplementation "junit:junit:4.12"
    testImplementation "org.robolectric:robolectric:" + rootProject.robolectricVersion
    testImplementation "androidx.test.espresso:espresso-core:" + rootProject.espressoVersion
    testImplementation "androidx.test.espresso:espresso-intents:" + rootProject.espressoVersion
    testImplementation "androidx.test.ext:truth:" + rootProject.extTruthVersion
}
